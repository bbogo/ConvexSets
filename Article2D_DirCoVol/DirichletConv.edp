// Minimization of Dirichlet-Laplace eigenvalues under 
// convexity and volume constraints in 2D
// 
// FreeFEM script. See installation instructions
// at freefem.org
//
// Example of usage:
// FreeFem++ -n 120 -i 0 -k 2
//
// Options: 
// -i   choose initialization
// -k   choose the index of the eigenvalue
// -n   choose the number of discretization points


load "Curvature"
load "medit"
load "ff-Ipopt"
load "distance"
include "macro_ddm.idp"                  // Additional DDM functions

// number of discretization points
int npts = getARGV("-n",120);

// Vector containing the variables
real[int] vect(npts);
vect = 0.5;

cout.precision(16);

// Choose the form of the objective function
// variant = 1 ->  lamdba*area
// variant = 2 ->  lambda+area
int variant=2;

// choose initialization (see below)
int ini = getARGV("-i",0);

// parameter for mesh adaptation
real adapth = 0.04;

// best known values (Antunes-Henrot, Antunes-Bogosel)
real[int] best = [0,37.987,0,65.28,79.70,88.54,109.44,120.58,137.38,143.15];

// size of the problem
int NN = vect.n;
// step for the uniform discretization
real h = 2*pi/NN;

// uniform discretization
real[int] thetas = 0:h:(2*pi-h/2);

// Initializations
for(int i=0;i<NN;i++){
  if(ini==0){
   vect[i] = 1+0.1*cos(2*thetas[i]);  // init for 2, 4
  }
  if(ini==1){
   vect[i] = 1+cos(2*thetas[i])*randreal1(); /// init for 5,7
  }
  if(ini==2){
     vect[i] = 1+0.1*cos(2*thetas[i])*randreal1(); /// init for 6,8,9
  }
  if(ini==3){
    vect[i] = 1+0.1*cos(3*thetas[i])*randreal1(); /// init for 10
  }	
  if(ini==4){
    vect[i] = 1+0.2*cos(6*thetas[i]);
               //+0.05*cos(2*thetas[i]); /// init for 10
  }	
  if(ini==5){
    vect[i] = 1+0.5*cos(3*thetas[i])*randreal1(); /// init for 10
  }
  if(ini==6){
    vect[i] = 1+0.15*cos(3*thetas[i])+0.05*randreal1()
              ; /// init for 10
  }	
  if(ini==7){
    vect[i] = 1+0.16*cos(3*thetas[i])+0.03*randreal1()
              ; /// init for 10
  }	
}

// the index of the eigenvalue
int eigCount = getARGV("-k",1);
int eString = eigCount-1;


real pm = 1;

// Choice of finite element spaces
macro Pk P2//
macro Pk0 P2//
macro Pkn P2//

real w = 2;
verbosity = 0;

// function producing a random vector
func real[int] rand(int h){
  randinit(randint32() );
  real[int] vec(h);
  for(int i = 0;i<h;i++){
     vec(i) = 2*randreal1()-1;
  }
  return vec;
}


// Number of constraints: 
// Convexity: nb of points

int Nconstr = vect.n;

// Constraint matrix
// clb <= A*x+bvec <= cub  
real[int,int] A(Nconstr,vect.n);
A = 0;  // initialization is important...

real fact = 2*cos(h);


for(int i=0;i<vect.n;i++){
  A(i,i) = -fact;
  if(i+1<vect.n){A(i,i+1) = 1;}
  if(i>0){A(i,i-1) = 1;}
}
A(0,vect.n-1) = 1;
A(vect.n-1,0) = 1;

// Matrix for evaluating the diameter
real[int,int] Adiam(vect.n/2,vect.n);
for(int i=0;i<vect.n/2;i++){
  Adiam(i,i) = 1;
  Adiam(i,i+vect.n/2) = 1;
}

real[int] bvec(Nconstr);
bvec = 0;

matrix B = A;

real[int] gradHold(vect.n);
real[int] gradPointEval(vect.n);

real minval = 1e16;
real[int] minHold(vect.n);

macro grad(u) [dx(u),dy(u)] // EOM

real[int,int] A0(vect.n,vect.n);
matrix B0;


real den1 = 2*sin(h);
real den2 = (2-2*cos(h));

// Computation of the objective function and
// its gradient

func real[int] f(real[int] &vect){

  real[int] tempvect = vect;
  real[int] ctr   = Adiam*vect;
  real diam = ctr.max;
  cout << "Diameter="<< diam << endl;
  real[int] vplus = [vect(1:NN-1),vect(0)];
  real[int] vmin  = [vect(NN-1),vect(0:NN-2)];
  real[int] deriv = (vplus-vmin);
  deriv = 1/den1*deriv;
  
  // Construct discrete polygon
  real[int] X(NN),Y(NN);
    for(int j=0;j<NN;j++){
       X[j] = cos(thetas(j))*vect(j)-sin(thetas(j))*deriv(j);
       Y[j] = sin(thetas(j))*vect(j)+cos(thetas(j))*deriv(j);
    }
  border G(t=0,1;i){
      int inext = (i+1)%NN;
      x = (1-t)*X[i]+t*X[inext];
      y = (1-t)*Y[i]+t*Y[inext];
      label=0;
  }
  int[int] NG(NN);
  NG = 2;
  //plot(G(NG));
  mesh Th;
  try{
     Th = buildmesh(G(NG),fixedborder=1);
     Th = adaptmesh(Th,hmax=adapth*diam,nbvx=50000,iso=1);
     Th = adaptmesh(Th,hmax=adapth*diam,nbvx=50000,iso=1);
  }
  catch(...){
    // If mesh generation fails, give a large value
    // and a random gradient
  	real[int] gradt = vect;
  	gradt = rand(vect.n);
  	real val = 1e16;
  	real[int] result = [val,gradt];
  	return result;
  }
  // Show current mesh
  plot(Th);
  fespace Vh(Th,Pk);
  fespace Vh0(Th,Pk0);
  Vh u,v,p;
  
  // Compute Dirichlet-Laplace eigenvalues
  varf aa(u,v) = int2d(Th)(dx(u)*dx(v)+dy(u)*dy(v))
                +on(0,u=0);
  varf bb(u,v) = int2d(Th)(u*v);

  real[int] ev(eigCount);
  Vh[int] eV(eigCount);
  real[int] eVect(Vh.ndof);
  real speed,lambda;
  matrix A = aa(Vh,Vh,solver=UMFPACK);
  matrix B = bb(Vh,Vh);
  int numEigs = EigenValue(A,B,sigma=0,sym=true,value=ev,vector=eV);
  
  // Sort the eigenvalues
  int[int] ii(0:ev.n-1);
  sort(ev,ii);

  // Store the desired eigenfunction and eigenvalue
  real eig = ev[eigCount-1];
  u[] = eV[ii(eigCount-1)][];
  
  // Plot if you want
  // plot(u,fill=1,wait=1);
  
  // Shape derivatives
  Vh0 ShDerivE = -(dx(u)^2+dy(u)^2);
  Vh0 ShDerivA = 1;
  
  // Compute Angles
  varf vtheta(u,v) = on(0,u=atan2(N.y,N.x));
  varf vNx(u,v) = on(0,u=N.x);
  varf vNy(u,v) = on(0,u=N.y);
  fespace Wh(Th,Pkn);
  Wh ThetaB = vtheta(0,Wh,tgv=1);
  Wh Nx = vNx(0,Wh,tgv=1);
  Wh Ny = vNy(0,Wh,tgv=1);
  Wh Norm = sqrt(Nx^2+Ny^2);
  Norm= Norm+1e-3*(Norm<1e-3);
  Nx = Nx/Norm;
  Ny = Ny/Norm;


  
  Vh0 ShDeriv;
  real val; 
  
  // Choose the objective function
  // Product (scale invariant) or sum
  // both giving equivalent optimal shapes
  // up to homotheties
  
  if(variant==1){
  	 ShDeriv = Th.area*ShDerivE+eig*ShDerivA; 
   	 val = eig*Th.area;
  }
  else{  
     ShDeriv = ShDerivE+ShDerivA; 
   	 val = eig+Th.area;
  }

  // Evaluate the gradient with respect to the 
  // parameters
  
  real[int] SDeval(NN);
  Vh[int] Chi(NN);
  for(int i=0;i<NN;i++){
     // Create hat function on the boundary
     Chi[i] = h-(acos(Nx*cos(thetas[i])+Ny*sin(thetas[i])));
     Chi[i] = 1/h*Chi[i]*(Chi[i]>=0);
     // Compute the sensibility for variable of index i
     SDeval[i] = int1d(Th,0)(ShDeriv*Chi[i]);
  }
  
  real[int] gradt = SDeval;
  real[int] result = [val,gradt];
 
  // change the sign for maximization problems
  
  result = pm*result;
  
  // Store the minimal value found in the optimization 
  // algorithm
  
  cout << "Act min val: " << minval << " AH = " << best[eigCount-1] << " Current= " << eig*Th.area << endl;
   if(eig*Th.area<minval){
      minval = eig*Th.area;
      minHold = vect;
      cout << "New min val: " << minval << endl;
      {
        ofstream fout("LamConvex"+(eigCount)+".dat");
        fout.precision(16);
		  fout << vect.n << endl;
		  for(int i=0;i<vect.n;i++){
 			 fout << minHold[i] << endl;
		  }
      };
      { ofstream f("minval"+eigCount+".dat");
  		f.precision(16);
  		f << minval << endl;
      }
   }
  return result;
}

f(vect);






// Create functions for IPOPT
// Objective function
func real J(real[int] &X){
   cout << "Eval J"<< endl;
   real[int] result = f(X);
   gradHold = result(1:result.n-1);
   gradPointEval = X;
   return result(0);
}

// Gradient
func real[int] gradJ(real[int] &X){
   cout << "Eval gradJ"<< endl;
   {
    ofstream f("EigConvex_ShDeriv.data");
    f.precision(16);
    for(int i=0;i<X.n;i++){
    	f << X[i] << endl;
    }
}
   real[int] dvec = X-gradPointEval;
   dvec = abs(dvec);
   if(dvec.min<1e-6)
     {
     cout << "Same grad" << endl;
     return gradHold;
     }
   else{
     real[int] result = f(X);
     gradHold = result(1:result.n-1);
     gradPointEval = X;
     return gradHold;
   }
}

// Vectors for lower and upper bounds
// for variables and constraints
real[int] clb = bvec;
clb = 0;
real[int] lb = vect;
real[int] ub = vect;
lb = 0.1;
ub = 2;

IPOPT(J,gradJ,[B,bvec],vect,clb=clb,lb=lb,linesearch=0,maxiter=1000);

// Evaluate the objective function at the end
// of the optimization process
real[int] rrr = f(vect);
cout << rrr(0) << endl;

// Save the last vector (possibly not the best one
// since linesearch=0)
{
    ofstream f("EigConvex_ShDerivMod"+(eigCount)+".data");
    f.precision(16);
    for(int i=0;i<vect.n;i++){
    	f << vect[i] << endl;
    }
}

string ss = minval;
cout << ss << endl;

// Copy the optimization history to a file
system("cp ipopt.out ipopt"+eigCount+".out");



